-- https://github.com/GPep/ToolsDB/blob/master/SPs/CheckDiskSpace.sql
USE [Tools]
GO


SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

IF OBJECT_ID('CheckDiskSpace') IS NULL
  EXEC ('CREATE PROCEDURE CheckDiskSpace AS RETURN 0;')
GO


ALTER PROCEDURE CheckDiskSpace
AS
BEGIN

-- =============================================
-- Author:		Glenn Pepper
-- Create date: 2018-09-20
-- Version 1.0
-- Description:	This Stored Procedure looks all disks for free space. 
-- Anything below 10% is reported to the DBA Team
-- =============================================

TRUNCATE TABLE Tools.dbo.DiskSize

INSERT INTO Tools.dbo.DiskSize(ServerName, DBNAME, Logical_File, Drive, Size_in_MB, FreeSpace_In_MB, FreePercentage, CheckDATE)
SELECT DISTINCT @@SERVERNAME as 'ServerName',
			DB_NAME(dovs.database_id) DBName,
			dovs.logical_volume_name AS LogicalName,
			dovs.volume_mount_point AS Drive,
			total_bytes/1048576 as Size_in_MB, 
			available_bytes/1048576 as FreeSpace_in_MB,
			(select ((available_bytes/1048576* 1.0)/(total_bytes/1048576* 1.0) *100)) as FreePercentage,
			GETDATE() AS 'CheckDate'
		FROM sys.master_files mf
			CROSS APPLY sys.dm_os_volume_stats(mf.database_id, mf.FILE_ID) dovs

		------
		-- SEND DBA TEAM AN EMAIL IF THE DRIVE SPACE IS BELOW 10%
		------
		IF (SELECT COUNT(*) FROM tools.dbo.DiskSize
WHERE FreePercentage <=10 ) > 0

BEGIN

		DECLARE @SERVERNAME NVARCHAR(50);
		DECLARE @EMAIL_SUBJECT NVARCHAR(250);
		DECLARE @EMAILADDRESS NVARCHAR(200);

		SET @SERVERNAME = @@SERVERNAME
		SET @EMAIL_SUBJECT = N'LOW DRIVE SPACE ON ' + @SERVERNAME
		SET @EMAILADDRESS = (SELECT Email_Address FROM msdb.dbo.sysoperators WHERE enabled = 1)

		DECLARE @MSG VARCHAR(MAX), @TBL VARCHAR(MAX), @TBL2 VARCHAR(MAX), @TBL3 VARCHAR(MAX)

	SET @TBL = '<FONT FACE=VERDANA SIZE=2><B>The drive indicated below needs investigating before it fills up as its below 10%</B></FONT><BR /><BR />
			<STYLE TYPE="TEXT/CSS">H2, BODY {FONT-FAMILY: ARIAL, VERDANA;} TABLE{FONT-SIZE:11PX; BORDER-COLLAPSE:COLLAPSE;} TD{BACKGROUND-COLOR:#FFFFFF; BORDER:1PX SOLID BLACK; PADDING:3PX;} TH{BACKGROUND-COLOR:#46F610;BORDER:1PX SOLID BLACK; PADDING:3PX;}</STYLE>
			<TABLE CELLPADDING=2 CELLSPACING=2><TR><TH><B>SERVER NAME</B></TH><TH><B>DRIVE LOCATION</B></TH><TH><B>FREE SPACE LEFT IN MB</B></TH><TH><B>FREE SPACE LEFT IN %</B></TH><TH><B>DATE</B></TH></TR>'
			+ 
			CAST((SELECT DISTINCT TD = SERVERName , ''
				, TD = DRIVE, ''
				, TD = FREESPACE_IN_MB, ''
				, TD = FreePercentage, ''
				, TD = CheckDATE, ''
				FROM TOOLS.DBO.DISKSIZE
				 FOR XML PATH('TR'), TYPE
				) AS VARCHAR(MAX) )
			+ '</TABLE><BR>

			<FONT FACE=VERDANA SIZE=2><B>If the server is about to fail then notify either the senior dba or infrastructure team about the size and urgency on this particular server</B></FONT><BR /><BR />'

		EXEC MSDB.DBO.SP_SEND_DBMAIL 
			@PROFILE_NAME = @SERVERNAME,
			@SUBJECT = @EMAIL_SUBJECT, 
			@BODY = @TBL, @BODY_FORMAT = 'HTML', 
			@RECIPIENTS = @EMAILADDRESS

END

END
